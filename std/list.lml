

open Std.Adt.Option (Option)
open Std.Compare ((==), (!=))
open Std.Arith ((+), (-))
open Std.String ((++), from)


#[extern("__rml_sys_list_add"), export]
let (:) (val: 'a) (arr: ['a]) : ['a]

#[export]
let map f list =
  when list is
    | [] = []
    | h:rest = f h : map f rest


#[export]
let filter f list =
  when list is
    | [] = []
    | h:rest =
       if f h then
         h:filter f rest
       else filter f rest

#[export]
let length arr =
  when arr is
    | [] = 0
    | _:t = 1 + length t

#[export]
let reduce acc func list =
  when list is 
    | [] = acc
    | h:rest = 
       let result = func acc h
       in reduce result func rest

#[export]
let nth index list =
  when list is
    | [] = None
    | [h] = if index == 0 then Some h else None
    | h:rest =
        if index == 0 then
          Some h
        else
          nth (index - 1) rest


#[export]
let (@) list index = nth index list

#[export]
let join list =
  when list is
   | [] = ""
   | h:rest =
     from h ++ join rest

#[export]
let joinBy delim list =
  when list is
   | [] = ""
   | [a] = from a
   | h:rest =
     from h ++ delim ++ joinBy delim rest

#[export]
let fromValue a = a:[]


